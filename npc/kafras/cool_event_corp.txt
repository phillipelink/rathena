//===== rAthena Script ======================================= 
//= Cool Event Corp. Staff
//===== By: ================================================== 
//= Gepard
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Cool Event Corp. Staff (Save, Storage & Pushcart Service)
//= in Arunafeltz Region.
//===== Additional Comments: ================================= 
//= 1.0 First version, iRO Sakray X.4 [Gepard]
//= 1.1 Optimized, fixed a conditional bug [Lupus]
//= 1.1a Redo back. It wasn't a bug [Lupus]
//= 1.2 Added Rachel Cool Event Corp. Staff [Playtester]
//= 1.3 Added cutins, closed Kafra Password exploit
//=     until we got a client/packet based password support [Lupus]
//= 1.4 Added Veins Cool Event Corp. Staff. [L0ne_W0lf]
//= 1.5 Cool Event Corp staff is now more iRO like. [L0ne_W0lf]
//= 1.6 Wrote a basic function and added VIP features. [Euphy]
//============================================================ 

//callfunc "F_CoolEventCorp","<menu>","<location>","<save map>",<x>,<y>,"<warp map>";
function	script	F_CoolEventCorp	{
    cutin "zonda_01", 2;
    mes "[Cool Event Corp. Staff]";
    mes "Bem-vindo à Cool Event Corp.";
    mes "Nossa equipe está sempre trabalhando";
    mes "para superar suas expectativas";
    mes "de serviço de qualidade. Como";
    mes "posso ajudá-lo hoje?";
    next;
    switch (select(getarg(0))) {
        case 1:
            mes "[Cool Event Corp. Staff]";
            mes "Seu ponto de respawn";
            mes "foi salvo aqui";
            mes getarg(1) + ".";
            mes "Obrigado por usar o";
            mes "serviço da Cool Event Corp.~";
            savepoint getarg(2), getarg(3), getarg(4), 1, 1;
            close2;
            break;
        case 2:
            mes "[Cool Event Corp. Staff]";
            if (!callfunc("F_CanOpenStorage")) {
                mes "Desculpe, mas você";
                mes "precisa do Nível 6 de";
                mes "Habilidade Básica de Noviço";
                mes "para usar o Serviço de Armazenamento.";
                close2;
                break;
            }
            if (Zeny < 40) {
                mes "Desculpe, mas você não";
                mes "tem Zeny suficiente para usar";
                mes "o Serviço de Armazenamento. Nossa";
                mes "taxa de acesso ao armazenamento é 40 Zeny.";
                close2;
                break;
            }
            set Zeny, Zeny - 40;
            mes "Deixe-me abrir seu";
            mes "armazenamento pessoal";
            mes "para você imediatamente.";
            mes "Obrigado por apoiar a Cool";
            mes "Event Corp. usando nossos";
            mes "serviços. Tenha um bom dia~";
            callfunc("F_CheckKafCode"); //verifique sua senha de armazenamento, se configurada
            close2;
            openstorage;
            break;
        case 3:
		mes "Por favor, escolha";
		mes "seu destino.";
		next;
		if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE))
			set .@cost, 4400;
		else
			set .@cost, 2200;
		.@destination$ = getarg(5);
		if (select(.@destination$ + " -> " + .@cost + " z:Cancelar") == 1) {
			if ((countitem(7060) != 0) || (Zeny >= .@cost)) {
				if (countitem(7060) != 0) {
					delitem 7060, 1; //Warp_Free_Ticket
				} else {
					set Zeny, Zeny - .@cost;
				}
				if (.@destination$ == "Veins") warp "veins", 205, 101;
				else if (.@destination$ == "Rachel") warp "rachel", 115, 125;
				cutin "", 255;
				end;
			} else {
				mes "[Cool Event Corp. Staff]";
				mes "Desculpe, mas você não tem";
				mes "zeny suficiente para o serviço de";
				mes "teleporte. A taxa para teletransportar";
				mes "para " + .@destination$ + " é " + .@cost + " zeny.";
			}
		}
		close2;
		break;
		case 4:
		mes "[Cool Event Corp. Staff]";
		if (BaseClass != Job_Merchant) {
			mes "Desculpe, mas o";
			mes "serviço de aluguel";
			if (checkre(0))
				mes "de carrinho só está disponível para classes de Comerciante.";
			else {
				mes "de carrinho só está disponível para Comerciantes,";
				mes "Ferreiros, Ferreiros Mestres,";
				mes "Alquimistas e Criadores.";
			}
			close2;
			break;
		}
		if (getskilllv("MC_PUSHCART") == 0) {
			mes "Você só pode alugar um carrinho depois de";
			mes "aprender a habilidade Carrinho de Mão.";
			close2;
			break;
		}
		if (checkcart() == 1) {
			mes "Você já possui";
			mes "um carrinho equipado.";
			mes "Infelizmente, não podemos";
			mes "alugar mais de um para";
			mes "cada cliente de uma vez.";
			close2;
			break;
		}
		if (countitem(7061)) {
			delitem 7061, 1; //Cart_Free_Ticket
			setcart;
			close2;
			break;
		}
		mes "A taxa de aluguel";
		mes "do carrinho é 800 Zeny. Gostaria";
		mes "de alugar um carrinho?";
		next;
		if (select("Alugar um Carrinho:Cancelar") == 1) {
			if (Zeny < 800) {
				mes "[Cool Event Corp. Staff]";
				mes "Desculpe, mas você";
				mes "não tem dinheiro";
				mes "suficiente para pagar a taxa de";
				mes "aluguel do carrinho de 800 Zeny.";
			} else {
				set Zeny, Zeny - 800;
				setcart;
			}
		}
		close2;
		break;
	case 5:
		callfunc("F_SetKafCode","[Cool Event Corp. Staff]","Cool Event Corp.");
		//the function closes msg and removes cutins
	case 6:
	default:
		mes "[Cool Event Corp. Staff]";
		mes "Cool Event Corp. está sempre";
		mes "se esforçando para fornecer os melhores";
		mes "serviços para nossos clientes.";
		mes "Ajude-nos a nos tornarmos os melhores";
		mes "fornecendo-nos suas opiniões";
		mes "e feedback honesto. Obrigado.";
		close2;
	}
	cutin "", 255;
	end;
}

lighthalzen,94,248,3	script	Cool_Event_Corp_Staff::Cool_Event_Corp_Staff_Lighthalzen	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"Salvar:Usar Armazenamento::Alugar um Carrinho de Mão:Serviço de Senha do Armazenamento:Cancelar",
		"na cidade de Lighthalzen","lighthalzen",95,240;
	end;
}

hugel,88,168,4	script	Cool_Event_Corp_Staff::Cool_Event_Corp_Staff_Hugel	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"Salvar:Usar Armazenamento::Alugar um Carrinho de Mão:Serviço de Senha do Armazenamento:Cancelar",
		"na vila de Hugel","hugel",96,155;
	end;
}

rachel,109,138,5	script	Cool_Event_Corp_Staff::Cool_Event_Corp_Staff_Rachel	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"Salvar:Usar Armazenamento:Serviço de Teleporte:Alugar um Carrinho de Mão:Serviço de Senha do Armazenamento:Cancelar",
		"na cidade de Rachel","rachel",113,137,"Veins";
	end;
}

veins,208,128,5	script	Cool_Event_Corp_Staff::Cool_Event_Corp_Staff_Veins	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"Salvar:Usar Armazenamento:Serviço de Teleporte:Alugar um Carrinho de Mão:Serviço de Senha do Armazenamento:Cancelar",
		"na cidade de Veins","veins",204,103,"Rachel";
	end;
}
