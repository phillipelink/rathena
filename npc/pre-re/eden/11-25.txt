//===== rAthena Script =======================================
//= Eden Group Quests - Mission [10 - 25]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 10 - 25.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Bug fixes. [Capuche]
//= 1.4 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.5 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,36,38,3	script	Missões [10 - 25]#Tuto	4_BOARD3,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "[^6959CDQuadro de Missões^000000]";
		mes "Você não faz parte do grupo do Éden!";
		mes "Fale com o Secretário do Eden para fazer parte do grupo.";		
		close;
	}
	for ( .@i = 11124; .@i < 11134; .@i++ )
		if (checkquest(.@i,PLAYTIME) == 2) erasequest .@i;
	for( .@i = 11114; .@i < 11124; .@i++ ) {
		.@j = checkquest(.@i,HUNTING);
		if (.@j == 2) {
			mes "[^6959CDQuadro de Missões^000000]";
			mes "^DF0D3AAtenção ^000000!!";
			mes "Antes de iniciar uma nova missão você precisa pegar sua recompensa.";
			mes "Fale imediatamente com a Gerente Spike localizada nessa sala.";
			close;
		}
		else if (.@j >= 0) {
			mes "[^6959CDQuadro de Missões^000000]";
			mes "Você só pode solicitar uma missão de cada vez.";
			close;
		}
	}
	if (BaseLevel < 10) {
		mes "[^6959CDQuadro de Missões^000000]";
		mes "^FF0000Você esta muito fraco para realizar as missões dessa fase^000000.";
		mes "^FF8C00Chegue ao Nível 10 de Base^000000!";
		close;
	}
	if (BaseLevel > 25) {
		mes "[^6959CDQuadro de Missões^000000]";
		mes "Você esta acima do nível de base requerido para fazer as missões.";
		mes "Procure um quadro apropriado para o seu nível de base.";
		close;
	}
	mes "[^6959CDQuadro de Missões^000000]";
	mes "^FF1493Missões de Caça Para Iniciantes^000000";
	mes "^708090Quadro Negro com várias Missões para aventureiros novatos.^000000";
	mes "Recomendado para: ^6959CDNível 10 ~ 25^000000";
	mes "Recompensa: ^0483DB1x Medalha do Edén^000000";
	mes "Recompensa: ^0483DBExp de Base^000000";

	//Verificar Quest Caça aos Hornets
	.@quest_status1 = checkquest(11114+10,PLAYTIME);
	if (.@quest_status1 == 0 || .@quest_status1 == 1) 
	{
		.@questHornet$ = "^009900[•]^000000 ^DC6401Caça aos Hornets^000000 - ^3B8706Concluida^000000";
	}
	else
	{
		.@questHornet$ = "^009900[•]^000000 ^DC6401Caça aos Hornets^000000 - ^FF0000Pendente^000000";
	}
	
	//Verificar Quest Caça aos Condors
	.@quest_status2 = checkquest(11115+10,PLAYTIME);
	if (.@quest_status2 == 0 || .@quest_status2 == 1) 
	{
		.@questCondor$ = "^009900[•]^000000 ^DC6401Caça aos Condors^000000 - ^3B8706Concluida^000000";
	}
	else
	{
		.@questCondor$ = "^009900[•]^000000 ^DC6401Caça aos Condors^000000 - ^FF0000Pendente^000000";
	}
	
	//Verificar Quest Caça as Pernas de Gafanhoto
	.@quest_status3 = checkquest(11116+10,PLAYTIME);
	if (.@quest_status3 == 0 || .@quest_status3 == 1) 
	{
		.@questGafanhoto$ = "^009900[•]^000000 ^DC6401Caça aos Gafanhotos^000000 - ^3B8706Concluida^000000";
	}
	else
	{
		.@questGafanhoto$ = "^009900[•]^000000 ^DC6401Caça aos Gafanhotos^000000 - ^FF0000Pendente^000000";
	}
	
	//Verificar Quest Caça aos Vermes
	.@quest_status4 = checkquest(11117+10,PLAYTIME);
	if (.@quest_status4 == 0 || .@quest_status4 == 1) 
	{
		.@questCaudaVerme$ = "^009900[•]^000000 ^DC6401Caça aos Vermes^000000 - ^3B8706Concluida^000000";
	}
	else
	{
		.@questCaudaVerme$ = "^009900[•]^000000 ^DC6401Caça aos Vermes^000000 - ^FF0000Pendente^000000";
	}
	
	//Verificar Quest Caça aos Esporos
	.@quest_status5 = checkquest(11118+10,PLAYTIME);
	if (.@quest_status5 == 0 || .@quest_status5 == 1) 
	{
		.@questEsporos$ = "^009900[•]^000000 ^DC6401Caça aos Esporos^000000 - ^3B8706Concluida^000000";
	}
	else
	{
		.@questEsporos$ = "^009900[•]^000000 ^DC6401Caça aos Esporos^000000 - ^FF0000Pendente^000000";
	}
	
	//Verificar Quest Controle de Pragas
	.@quest_status6 = checkquest(11119+10,PLAYTIME);
	if (.@quest_status6 == 0 || .@quest_status6 == 1) 
	{
		.@questControlePragas$ = "^009900[•]^000000 ^DC6401Controle de Pragas^000000 - ^3B8706Concluida^000000";
	}
	else
	{
		.@questControlePragas$ = "^009900[•]^000000 ^DC6401Controle de Pragas^000000 - ^FF0000Pendente^000000";
	}
	
	//Verificar Quest Caça aos Mukas
	.@quest_status7 = checkquest(11120+10,PLAYTIME);
	if (.@quest_status7 == 0 || .@quest_status7 == 1) 
	{
		.@questMuka$ = "^009900[•]^000000 ^DC6401Caça aos Mukas^000000 - ^3B8706Concluida^000000";
	}
	else
	{
		.@questMuka$ = "^009900[•]^000000 ^DC6401Caça aos Mukas^000000 - ^FF0000Pendente^000000";
	}
	
	//Verificar Quest Caça aos Familiares
	.@quest_status8 = checkquest(11121+10,PLAYTIME);
	if (.@quest_status8 == 0 || .@quest_status8 == 1) 
	{
		.@questFamiliar$ = "^009900[•]^000000 ^DC6401Caça aos Familiares^000000 - ^3B8706Concluida^000000";
	}
	else
	{
		.@questFamiliar$ = "^009900[•]^000000 ^DC6401Caça aos Familiares^000000 - ^FF0000Pendente^000000";
	}
	
	//Verificar Quest Coletar Penas
	.@quest_status9 = checkquest(11122+10,PLAYTIME);
	if (.@quest_status9 == 0 || .@quest_status9 == 1) 
	{
		.@questPena$ = "^009900[•]^000000 ^DC6401Coletar Penas^000000 - ^3B8706Concluida^000000";
	}
	else
	{
		.@questPena$ = "^009900[•]^000000 ^DC6401Coletar Penas^000000 - ^FF0000Pendente^000000";
	}
	
	//Verificar Quest Coletar Esporos Venenosos
	.@quest_status10 = checkquest(11123+10,PLAYTIME);
	if (.@quest_status10 == 0 || .@quest_status10 == 1) 
	{
		.@questColetarEsporos$ = "^009900[•]^000000 ^DC6401Coletar Esporos Venenosos^000000 - ^3B8706Concluida^000000";
	}
	else
	{
		.@questColetarEsporos$ = "^009900[•]^000000 ^DC6401Coletar Esporos Venenosos^000000 - ^FF0000Pendente^000000";
	}
	
	next;
	switch(select(.@questHornet$ + ":" + .@questCondor$ + ":" + .@questGafanhoto$ + ":" + .@questCaudaVerme$ + ":" + .@questEsporos$ + ":" + .@questControlePragas$ + ":" + .@questMuka$ + ":" + .@questFamiliar$ + ":" + .@questPena$ + ":" + .@questColetarEsporos$ + ":^FF0000[•]^000000 Sair ")) {
		case 1: callsub L_Quest,11114,"Caça aos Hornets", " - Caçar 10 ^4d4dff'Hornets'^000000 - ", " - ao noroeste de Prontera. - ";
		case 2: callsub L_Quest,11115,"Caça aos Condors"," - Caçar 20 ^4d4dff'Condors'^000000 - "," - ao Sul de Morocc. - ";
		case 3: callsub L_Quest,11116,"Caça aos Gafanhotos", " - Caçar 10 ^4d4dff'Rockers'^000000 - ", " - ao sudoeste de Prontera e - ", " - trazer 10 ^4d4dffPernas de Gafanhoto^000000. - ";
		case 4: callsub L_Quest,11117,"Caça aos Vermes"," - Caçar 20 ^4d4dff'Caudas de Verme'^000000 - "," - sudeste de Payon.- ";
		case 5: callsub L_Quest,11118,"Caça aos Esporos"," - Caçar 30 ^4d4dff'Esporos'^000000 - "," - ao redor de Payon. - ";
		case 6: callsub L_Quest,11119,"Controle de Pragas"," - Caçar 20 ^4d4dff'Ovos de Besouro Ladrão'^000000 - "," - nos esgotos de Prontera - "," - e trazer 10 ^4d4dffCrisalida^000000. - ";
		case 7: callsub L_Quest,11120,"Caça aos Mukas"," - Caçar 20 ^4d4dff'Mukas'^000000 - "," - ao Sul de Prontera. - ";
		case 8: callsub L_Quest,11121,"Caça aos Familiares"," - Caçar 20 ^4d4dffFamiliares^000000 - "," - nos Esgotos - "," - a oeste de Prontera. - ";
		case 9: callsub L_Quest,11122,"Coletar Penas"," - Caçar 30 ^4d4dff'Peco Pecos'^000000 - "," - ao Sul de Prontera. - ";
		case 10: callsub L_Quest,11123,"Coletar Esporos Venenosos"," - Caçar 30 ^4d4dffEsporos Venenosos^000000 - "," - dentro da Torre de Geffen e - "," - traga 5 ^4d4dffEsporos Venenosos^000000. - ";
		case 11:
			mes "[^6959CDQuadro de Missões^000000]";
			mes "Tudo bem, volte quando tiver interesse.";
			close;
	}

L_Quest:
	.@quest_status = checkquest(getarg(0)+10,PLAYTIME);
	if (.@quest_status == 0 || .@quest_status == 1) {
		mes "[^6959CDQuadro de Missões^000000]";
		//mes " - Missão: ^4d4dff"+ getarg(1) +"^000000 - ";
		mes "Esta missão já foi concluida por hoje.";
		mes "Tente outras missões.";
		close;
	}
	mes "[^6959CDQuadro de Missões^000000]";
	mes " - Missão '"+ getarg(1) +"' - ";
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	next;
	mes "[^6959CDQuadro de Missões^000000]";
	mes "Quando finalizar esta missão falar com a ^F76105Gerente Spike^000000 para pegar sua recompensa.";
	next;
	if (select("Eu farei esta missão.:Não quero essa missão.") == 2) {
		mes "[^6959CDQuadro de Missões^000000]";
		mes "Tudo bem, volte quando decidir qual missão deseja realizar.";
		close;
	}
	mes "[^6959CDQuadro de Missões^000000]";
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	setquest getarg(0);
	close;
	end;
	
	
	OnInit:
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(11114+10,PLAYTIME) == -1 && BaseLevel >= 10 && BaseLevel <= 25";
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(11115+10,PLAYTIME) == -1 && BaseLevel >= 10 && BaseLevel <= 25";
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(11116+10,PLAYTIME) == -1 && BaseLevel >= 10 && BaseLevel <= 25";
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(11117+10,PLAYTIME) == -1 && BaseLevel >= 10 && BaseLevel <= 25";
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(11118+10,PLAYTIME) == -1 && BaseLevel >= 10 && BaseLevel <= 25";
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(11119+10,PLAYTIME) == -1 && BaseLevel >= 10 && BaseLevel <= 25";
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(11120+10,PLAYTIME) == -1 && BaseLevel >= 10 && BaseLevel <= 25";
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(11121+10,PLAYTIME) == -1 && BaseLevel >= 10 && BaseLevel <= 25";
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(11122+10,PLAYTIME) == -1 && BaseLevel >= 10 && BaseLevel <= 25";
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(11123+10,PLAYTIME) == -1 && BaseLevel >= 10 && BaseLevel <= 25";
		end;
}

moc_para01,33,36,6	script	Gerente Spike	10383,{
	for( .@i = 11114; .@i < 11124; .@i++ ) {
		.@j = checkquest(.@i,HUNTING);
		if (.@j == 2) {
			switch(.@i) {
				case 11114: callsub L_Quest,.@i,"Caça aos Hornets",900;
				case 11115: callsub L_Quest,.@i,"Caça aos Condors",1800;
				case 11116: callsub L_Quest,.@i,"Caça aos Gafanhotos",1400,940,10;
				case 11117: callsub L_Quest,.@i,"Caça aos Vermes",2600;
				case 11118: callsub L_Quest,.@i,"Caça aos Esporos",3900;
				case 11119: callsub L_Quest,.@i,"Controle de Pragas",2900,915,10;
				case 11120: callsub L_Quest,.@i,"Caça aos Mukas",3200;
				case 11121: callsub L_Quest,.@i,"Caça aos Familiares",3200;
				case 11122: callsub L_Quest,.@i,"Coletar Penas",5400;
				case 11123: callsub L_Quest,.@i,"Coletar Esporos Venenosos",6000,7033,5;
			}
		}
		else if (.@j >= 0) {
			mes "[^3562DCGerente Spike^000000]";
			mes "Você ainda não terminou sua missão.";
			mes "Continue o bom trabalho. Você está quase lá.";
			close;
		}
	}
	mes "[^3562DCGerente Spike^000000]";
	mes "Você está procurando por um emprego?";
	mes "Nós sempre temos muito trabalho para fazer.";
	next;
	mes "[^3562DCGerente Spike^000000]";
	mes "Por que você não dá uma olhada por aí e encontra algumas tarefas interessantes?";
	close;

L_Quest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id,HUNTING) == 2) {
		.@item_req = getarg(3,0);
		.@req_amount = getarg(4,0);
		if (.@item_req) {
			if (countitem(.@item_req) < .@req_amount) {
				mes "[^3562DCGerente Spike^000000]";
				mes "Você não tem o suficiente "+ getitemname(.@item_req) +".";
				mes "Você precisa trazer "+ .@req_amount +" "+ getitemname(.@item_req) +".";
				close;
			}
		}
		mes "[^3562DCGerente Spike^000000]";
		mes "Hmm... Deixa me ver -_-";
		mes "Você completou a missão ^036C0C"+ getarg(1) +"^000000.";
		next;
		mes "[^3562DCGerente Spike^000000]";
		mes "Bom trabalho~!!";
		mes "Nosso cliente está muito feliz com seu trabalho. Ele irá apreciar o que você fez.";
		mes "Por favor, nos ajude novamente. Obrigado!";
		if (.@item_req)
			delitem .@item_req,.@req_amount;
		erasequest .@quest_id;
		setquest .@quest_id+10;
		getexp getarg(2),0;
		close;
	}
}
