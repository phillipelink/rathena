//===== rAthena Script =======================================
//= Sign Your Items
//===== By: ==================================================
//= Lupus
//===== Current Version: =====================================
//= 1.1a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Write you name on your rare equipment or weapon ^_-
//===== Additional Comments: =================================
//= 1.1 Cleaned and edited for general use. [Euphy]
//= 1.1a Added 'disable_items' command. [Euphy]
//============================================================

hero_dun1,126,188,4	script	Assinatura de Itens	47,{
	
	setarray .@Item[0],644,3;	// Item requirements: <ID>,<Count>{,...} (0 to disable)
	setarray .@Cost[0],0,5000;	// Zeny requirements: <base price>,<price per refine>

	disable_items;
	mes "[Tabelião]";
	mes "Posso ^0055FF Assinar seu nome^000000 em quase qualquer item raro que você possua.";
	next;
	if(select(" - Mais informações...: - Quero assinar meu item!") == 1) {
		mes "[Tabelião]";
		mes "Posso colocar seu nome em qualquer equipamento ou arma sem espaço para cartas.";
		emotion ET_AHA;
		next;
		mes "[Tabelião]";
		if (getarraysize(.@Item) || getarraysize(.@Cost)) {
			mes "Para realizar o meu trabalho, eu aceito:";
			if (getarraysize(.@Item)) for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2)
				mes " ~ "+.@Item[.@i+1]+"x "+getitemname(.@Item[.@i]);
			if (.@Cost[0]) mes " ~ "+callfunc("F_InsertComma",.@Cost[0])+" Zeny";
			if (.@Cost[1]) mes " ~ "+callfunc("F_InsertComma",.@Cost[1])+" Zeny por refinamento";
		}
		else  
		mes "Eu trabalho de graça, mas...";
		next;
		emotion ET_CRY;
		mes "[Tabelião]";
		mes "Ai, tenho 12 filhos famintos";
		mes "e uma esposa muito brava.";
		next;
		mes "[Tabelião]";
	    mes "Ou eram 12 filhos bravos";
		mes "e uma esposa faminta...";
		emotion ET_SCRATCH;
		close;
	}
	mes "[Tabelião]";
	mes "Mostre-me os itens que você deseja assinar...";
	next;
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for (set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@indices[.@i]))
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) +" [^0055FF"+getequipname(.@indices[.@i])+"^000000]";
		set .@menu$, .@menu$+":";
	}
	set .@part, .@indices[ select(.@menu$) ];
	set .@id, getequipid(.@part);
	set .@ref, getequiprefinerycnt(.@part);
	mes "[Tabelião]";
	if (!getequipisequiped(.@part)) {
		mes "Você não tem nada equipado!";
		emotion ET_KEK;
		close;
	}
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		set .@slot[.@i], getequipcardid(.@part,.@i);
	if (.@slot[0]==255 || .@slot[0]==254 || .@slot[0]<0) {
		mes "Ai, este item já está assinado.";
		mes "Eu nunca mexeria no trabalho de um mestre.";
		emotion ET_SCRATCH;
		close;
	}
	if (.@slot[0]>4000 && .@slot[0]<5000) {
		mes "Um carta? Aqui?!";
		mes "Como eu disse antes, eu não assino itens com cartas.";
		emotion ET_SCRATCH;
		close;
	}
	if (getiteminfo(.@id, ITEMINFO_SLOT)) {
		mes "Desculpe, eu não assino itens com slots.";
		emotion ET_SORRY;
		close;
	}
	getinventorylist;
	for(set .@i,0; .@i<@inventorylist_count; set .@i,.@i+1)
		if (@inventorylist_expire[.@i] != 0) {
			mes "Desculpe, eu não assino itens alugados!";
			emotion ET_SCRATCH;
			close;
		}
	set .@price, .@Cost[0]+(.@Cost[1]*.@ref);
	if (getarraysize(.@Item) || .@price) {
		mes "Eu vou precisar de:";
		if (getarraysize(.@Item)) for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2) {
			mes " ~ "+.@Item[.@i+1]+"x "+getitemname(.@Item[.@i]);
			if (countitem(.@Item[.@i]) < .@Item[.@i+1]) set .@nr,1;
		}
		if (.@price) mes " ~ "+.@price+" Zeny";
	}
	mes "Devo assinar o seu ^0055FF"+getitemname(.@id)+"^000000?";
	next;
	if(select("Ok!:Leave")==2) {
		mes "[Tabelião]";
		mes "Até logo...";
		emotion ET_SLEEPY;
		close;
	}
	mes "[Tabelião]";
	if (Zeny < .@price || .@nr) {
		mes "Eu não trabalho por 'agradecimentos'.";
		emotion ET_SORRY;
		close;
	}
	if (!countitem2(.@id,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3])) {
		mes "Onde está "+getitemname(@id)+"...?";
		npctalk "Tabelião : Você é um trapaceiro preguiçoso!";
		logmes "Hack: Tentou assinar um item que não possui: "+getitemname(@id);
		emotion ET_KEK;
		close;
	}
	if (.@price) set Zeny, Zeny-.@price;
	if (getarraysize(.@Item))  for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2)
		delitem .@Item[.@i], .@Item[.@i+1];
	delitem2 .@id,1,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3];
	mes "Feito!";
	emotion ET_OK;
	getitem2 .@id,1,1,.@ref,0,254,0,getcharid(0)&0xffff,(getcharid(0)>>16)&0xffff;
	equip .@id;
	close;

}



